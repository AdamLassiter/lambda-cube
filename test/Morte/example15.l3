-- corecursive.mt

-- data IOF r s = PutStrLn String s | GetLine (String -> s) | Return r
--
-- data IO r = π s . MkIO s (s -> IOF r s)
--
-- main = MkIO Nothing (maybe (\str -> PutStrLn str Nothing) (GetLine Just))

(   λ (String : *)
->  (   λ (Maybe : * -> *)
    ->  λ (Just : π (a : *) -> a -> Maybe a)
    ->  λ (Nothing : π (a : *) -> Maybe a)
    ->  λ (  maybe
        :   π (a : *) -> Maybe a -> π (x : *) -> (a -> x) -> x -> x
        )
    ->  λ (IOF : * -> * -> *)
    ->  λ (  PutStrLn
        :   π (r : *)
        ->  π (s : *)
        ->  String
        ->  s
        ->  IOF r s
        )
    ->  λ (  GetLine
        :   π (r : *)
        ->  π (s : *)
        ->  (String -> s)
        ->  IOF r s
        )
    ->  λ (  Return
        :   π (r : *)
        ->  π (s : *)
        ->  r
        ->  IOF r s
        )
    ->  (   λ (IO : * -> *)
        ->  λ (  MkIO
            :   π (r : *) -> π (s : *) -> s -> (s -> IOF r s) -> IO r
            )
        ->  (   λ (main : π (r : *) -> IO r)
            ->  main
            )

            -- main
            (   λ (r : *)
            ->  MkIO
                r
                (Maybe String)
                (Nothing String)
                (   λ (m : Maybe String)
                ->  maybe
                        String
                        m
                        (IOF r (Maybe String))
                        (λ (str : String) ->
                            PutStrLn r (Maybe String) str (Nothing String)
                        )
                        (GetLine r (Maybe String) (Just String))
                )
            )
        )

        -- IO
        (   λ (r : *)
        ->  π (x : *)
        ->  (π (s : *) -> s -> (s -> IOF r s) -> x)
        ->  x
        )

        -- MkIO
        (   λ (r : *)
        ->  λ (s : *)
        ->  λ (seed : s)
        ->  λ (step : s -> IOF r s)
        ->  λ (x : *)
        ->  λ (k : π (s : *) -> s -> (s -> IOF r s) -> x)
        ->  k s seed step
        )
    )

    -- Maybe
    (λ (a : *) -> π (x : *) -> (a -> x) -> x -> x)

    -- Just
    (   λ (a : *)
    ->  λ (va : a)
    ->  λ (x : *)
    ->  λ (Just : a -> x)
    ->  λ (Nothing : x)
    ->  Just va
    )

    -- Nothing
    (   λ (a : *)
    ->  λ (x : *)
    ->  λ (Just : a -> x)
    ->  λ (Nothing : x)
    ->  Nothing
    )

    -- maybe
    (λ (a : *) -> λ (m : π (x : *) -> (a -> x) -> x -> x) -> m)

    -- IOF
    (   λ (r : *)
    ->  λ (s : *)
    ->  π (x : *)
    ->  (String -> s -> x)
    ->  ((String -> s) -> x)
    ->  (r -> x)
    ->  x
    )

    -- PutStrLn
    (   λ (r : *)
    ->  λ (s : *)
    ->  λ (str : String)
    ->  λ (vs : s)
    ->  λ (x : *)
    ->  λ (PutStrLn : String -> s -> x)
    ->  λ (GetLine : (String -> s) -> x)
    ->  λ (Return : r -> x)
    ->  PutStrLn str vs
    )

    -- GetLine
    (   λ (r : *)
    ->  λ (s : *)
    ->  λ (k : String -> s)
    ->  λ (x : *)
    ->  λ (PutStrLn : String -> s -> x)
    ->  λ (GetLine : (String -> s) -> x)
    ->  λ (Return : r -> x)
    ->  GetLine k
    )

    -- Return
    (   λ (r : *)
    ->  λ (s : *)
    ->  λ (vr : r)
    ->  λ (x : *)
    ->  λ (PutStrLn : String -> s -> x)
    ->  λ (GetLine : (String -> s) -> x)
    ->  λ (Return : r -> x)
    ->  Return vr
    )

)
