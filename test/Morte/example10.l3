-- corecursive.mt

-- first :: (a -> b) -> (a, c) -> (b, c)
-- first f (va, vb) = (f va, vb) 
-- 
-- data Stream a = Cons (a, Stream a)
-- 
-- map :: (a -> b) -> Stream a -> Stream b
-- map f (Cons (va, s)) = Cons (first f (va, map f s))
-- 
-- -- exampleA = exampleB
-- 
-- exampleA :: Stream a -> Stream a
-- exampleA = map id
-- 
-- exampleB :: Stream a -> Stream a
-- exampleB = id
-- 
-- -- exampleC = exampleD
-- 
-- exampleC :: (b -> c) -> (a -> b) -> Stream a -> Stream c
-- exampleC f g = map (f . g)
-- 
-- exampleD :: (b -> c) -> (a -> b) -> Stream a -> Stream c
-- exampleD f g = map f . map g

(   λ (id : π (a : *) -> a -> a)
->  λ (  app
    :   π (a : *)
    ->  π (b : *)
    ->  π (c : *)
    ->  (b -> c)
    ->  (a -> b)
    ->  (a -> c)
    )
->  λ (Pair : * -> * -> *)
->  λ (P : π (a : *) -> π (b : *) -> a -> b -> Pair a b)
->  λ (  first
    :   π (a : *)
    ->  π (b : *)
    ->  π (c : *)
    ->  (a -> b)
    ->  Pair a c
    ->  Pair b c
    )

->  (   λ (Stream : * -> *)
    ->  λ (  map
        :   π (a : *)
        ->  π (b : *)
        ->  (a -> b)
        ->  Stream a
        ->  Stream b
        )

        -- exampleA = exampleB
    ->  (   λ (exampleA : π (a : *) -> Stream a -> Stream a)
        ->  λ (exampleB : π (a : *) -> Stream a -> Stream a)

        -- exampleC = exampleD
        ->  λ (  exampleC
            :   π (a : *)
            ->  π (b : *)
            ->  π (c : *)
            ->  (b -> c)
            ->  (a -> b)
            ->  Stream a
            ->  Stream c
            )

        ->  λ (  exampleD
            :   π (a : *)
            ->  π (b : *)
            ->  π (c : *)
            ->  (b -> c)
            ->  (a -> b)
            ->  Stream a
            ->  Stream c
            )

        -- Uncomment the example you want to test
        ->  exampleA
--      ->  exampleB
--      ->  exampleC
--      ->  exampleD
        )

        -- exampleA
        (λ (a : *) -> map a a (id a))
  
        -- exampleB
        (λ (a : *) -> id (Stream a))

        -- exampleC
        (   λ (a : *)
        ->  λ (b : *)
        ->  λ (c : *)
        ->  λ (f : b -> c)
        ->  λ (g : a -> b)
        ->  map a c (app a b c f g)
        )

        --  exampleD
        (   λ (a : *)
        ->  λ (b : *)
        ->  λ (c : *)
        ->  λ (f : b -> c)
        ->  λ (g : a -> b)
        ->  app (Stream a) (Stream b) (Stream c) (map b c f) (map a b g)
        )
    )

    -- Stream
    (   λ (a : *)
    ->  π (x : *)
    ->  (π (s : *) -> s -> (s -> Pair a s) -> x)
    ->  x
    )

    -- map
    (   λ (a : *)
    ->  λ (b : *)
    ->  λ (f : a -> b)
    ->  λ (  st
        :   π (x : *) -> (π (s : *) -> s -> (s -> Pair a s) -> x) -> x
        )
    ->  λ (x : *)
    ->  λ (S : π (s : *) -> s -> (s -> Pair b s) -> x)
    ->  st
        x
        (   λ (s : *)
        ->  λ (seed : s)
        ->  λ (step : s -> Pair a s)
        ->  S
            s
            seed
            (λ (seed : s) -> first a b s f (step seed))
        )
    )
)

-- id
(λ (a : *) -> λ (va : a) -> va)

-- app
(   λ (a : *)
->  λ (b : *)
->  λ (c : *)
->  λ (f : b -> c)
->  λ (g : a -> b)
->  λ (va : a)
->  f (g va)
)

-- Pair
(λ (a : *) -> λ (b : *) -> π (x : *) -> (a -> b -> x) -> x)

-- P
(   λ (a : *)
->  λ (b : *)
->  λ (va : a)
->  λ (vb : b)
->  λ (x : *)
->  λ (P : a -> b -> x)
->  P va vb
)

-- first
(   λ (a : *)
->  λ (b : *)
->  λ (c : *)
->  λ (f : a -> b)
->  λ (p : π (x : *) -> (a -> c -> x) -> x)
->  λ (x : *)
->  λ (Pair : b -> c -> x)
->  p x (λ (va : a) -> λ (vc : c) -> Pair (f va) vc)
)
