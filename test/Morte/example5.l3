-- all.mt

-- let data Bool = True | False
--
--     data List a = Cons a (List a) | Nil
--
-- in  let and :: Bool -> Bool -> Bool
--         and b1 b2 = if b1 then b2 else False
--
--         bools :: List Bool
--         bools = Cons True (Cons True (Cons True Nil))
--
--     in  foldr bools and True

(   λ (Bool : *)
->  λ (True  : Bool)
->  λ (False : Bool)
->  λ (if : Bool -> π (r : *) -> r -> r -> r)
->  λ (List : * -> *)
->  λ (Cons : π (a : *) -> a -> List a -> List a)
->  λ (Nil  : π (a : *)                -> List a)
->  λ (  foldr
    :   π (a : *) -> List a -> π (r : *) -> (a -> r -> r) -> r -> r
    )
->  (   λ (and : Bool -> Bool -> Bool)
    ->  λ (bools : List Bool)
    ->  foldr Bool bools Bool and True
    )

    -- and
    (λ (x : Bool) -> λ (y : Bool) -> if x Bool y False)

    -- bools
    (Cons Bool True (Cons Bool True (Cons Bool True (Nil Bool))))
)

-- Bool
(π (r : *) -> r -> r -> r)

-- True
(λ (r : *) -> λ (x : r) -> λ (_ : r) -> x)

-- False
(λ (r : *) -> λ (_ : r) -> λ (x : r) -> x)

-- if
(λ (b : π (r : *) -> r -> r -> r) -> b)

-- List
(   λ (a : *)
->  π (list : *)
->  (a -> list -> list)  -- Cons
->  list                 -- Nil
->  list
)

-- Cons
(   λ (a : *)
->  λ (va  : a)
->  λ (vas : π (list : *) -> (a -> list -> list) -> list -> list)
->  λ (list : *)
->  λ (Cons : a -> list -> list)
->  λ (Nil  : list)
->  Cons va (vas list Cons Nil)
)

-- Nil
(   λ (a : *)
->  λ (list : *)
->  λ (Cons : a -> list -> list)
->  λ (Nil  : list)
->  Nil
)

-- foldr
(   λ (a : *)
->  λ (vas : π (list : *) -> (a -> list -> list) -> list -> list)
->  vas
)
