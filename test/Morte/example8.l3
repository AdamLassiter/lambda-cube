-- mapcomp1.mt

-- map (f . g)

(   λ (List : * -> *)
->  λ (map  : π (a : *) -> π (b : *) -> (a -> b) -> List a -> List b)
->  λ (  app
    :   π (a : *)
    ->  π (b : *)
    ->  π (c : *)
    ->  (b -> c)
    ->  (a -> b)
    ->  (a -> c)
    )
    ->  λ (a : *)
    ->  λ (b : *)
    ->  λ (c : *)
    ->  λ (f : b -> c)
    ->  λ (g : a -> b)
    ->  map a c (app a b c f g)
)

-- List
(λ (a : *) -> π (x : *) -> (a -> x -> x) -> x -> x)

-- map
(   λ (a : *)
->  λ (b : *)
->  λ (f : a -> b)
->  λ (l : π (x : *) -> (a -> x -> x) -> x -> x)
->  λ (x : *)
->  λ (Cons : b -> x -> x)
->  λ (Nil: x)
->  l x (λ (va : a) -> λ (vx : x) -> Cons (f va) vx) Nil
)

-- app
(   λ (a : *)
->  λ (b : *)
->  λ (c : *)
->  λ (f : b -> c)
->  λ (g : a -> b)
->  λ (va : a)
->  f (g va)
)
